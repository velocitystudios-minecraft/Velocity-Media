plugins {
	id "net.minecraftforge.gradle"
	id "org.parchmentmc.librarian.forgegradle"
	id "me.modmuss50.mod-publish-plugin"
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "8.1.1"
	id "idea"
}


group = "fr.velocity"
base.archivesName = 'velocity'
version = '2.1.9'



sourceSets {
	main {
		java
		resources
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
	withSourcesJar()
	withJavadocJar()
}

configurations {
	shade
	tinyfd
	library
	library.extendsFrom(shade)
	library.extendsFrom(tinyfd)
	minecraftLibrary.extendsFrom(library)
}

minecraft {
	mappings channel: 'stable', version: '39-1.12'
	
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	
	runs {
		client {
			workingDirectory file("run/client")
			property "musicplayer.dev", lavaplayerLibraries()
			
			mods {
				"velocitymedia" {
					source sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory file("run/server")
			property "musicplayer.dev", lavaplayerLibraries()
			
			mods {
				"velocitymedia" {
					source sourceSets.main
				}
			}
		}
	}
}

minecraft.getRuns().each { task ->
	task.environment "MC_VERSION", "{mc_version}"
}

evaluationDependsOn(":musicplayer-lavaplayer")

tasks.matching { task -> task.name == "prepareRuns" }.configureEach {
	dependsOn project(":musicplayer-lavaplayer").tasks.named("assemble")
}

tasks.register("deleteTinyfd", Delete) {
	delete layout.buildDirectory.dir("tinyfd").map { it.asFileTree }
}

tasks.register("copyTinyfd", Copy) {
	dependsOn tasks.named("deleteTinyfd")
	
	from configurations.tinyfd
	into layout.buildDirectory.dir("tinyfd")
	rename { fileName ->
		fileName += ".packed"
	}
}

tasks.named("shadowJar").configure {
	dependsOn tasks.named("jar")
	dependsOn tasks.named("copyTinyfd")
	dependsOn project(":musicplayer-lavaplayer-api").tasks.named("assemble")
	dependsOn project(":musicplayer-lavaplayer").tasks.named("assemble")
	
	archiveClassifier = tasks.named("jar").flatMap { jar -> jar.archiveClassifier }
	destinationDirectory = tasks.named("jar").flatMap { jar -> jar.destinationDirectory }
	
	configurations = [project.configurations.shade]
	
	from sourceSets.main.output
	from project(":musicplayer-lavaplayer").file("src/main/resources/LICENSE-DEPENDENCIES")
	
	doFirst {
		from project(":musicplayer-lavaplayer-api").sourceSets.main.output
	}
	
	into ("dependencies") {
		final def buildDir = project(":musicplayer-lavaplayer").layout.buildDirectory
		from buildDir.dir("libs").get().file("musicplayer-lavaplayer.jar.packed")
		from buildDir.dir("dependencies")
	}
	
	into ("tinyfd") {
		from project.layout.buildDirectory.dir("tinyfd")
	}
	
	relocate "org.slf4j", "fr.velocity.music.shade.org.slf4j"
	relocate "net.harawata.appdirs", "fr.velocity.music.shade.net.harawata.appdirs"
	
	mergeServiceFiles()
	
	exclude "META-INF/maven/**"
	exclude "META-INF/versions/**"
}

components.named("java").configure {
	withVariantsFromConfiguration(configurations.named("shadowRuntimeElements").get()) {
		skip()
	}
}

reobf.create("jar") {
	dependsOn tasks.named("shadowJar")
}

//tasks.withType(Jar).configureEach { task ->
//	gradlefiles.defaultJar(task)
//}

tasks.withType(Javadoc).configureEach { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

//signjar.sign(tasks.named("reobfJar"))

processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
			file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
		}
	}

	inputs.property "version", '2.1.9'
	inputs.property "mcversion", '1.12.2'

	filesMatching("mcmod.info") {
		expand "version": '2.1.9', "mcversion": '1.12.2'
	}
}

tasks.register("prepareSources", Copy) {
	from sourceSets.main.java
	into file("${buildDir}/prepareSources")
	filter (org.apache.tools.ant.filters.ReplaceTokens, tokens: [
		"VERSION": "2.1.9",
		"MCVERSION": "1.12.2"
	])
	outputs.upToDateWhen { false }
}

tasks.named("compileJava").configure { task ->
	final def prepareResources = tasks.named("prepareSources")
	task.dependsOn prepareResources
	task.source = prepareResources.get().destinationDir
}

processResources {
	inputs.property "version", '2.1.9'
	inputs.property "mcversion", '1.12.2'
	
	filesMatching("mcmod.info") {
		expand "version": '2.1.9', "mcversion": '1.12.2'
	}
}

repositories {
	maven {
		url = "https://www.cursemaven.com"
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

	library project(":musicplayer-lavaplayer-api")
	shade "org.slf4j:slf4j-simple:2.0.16"
	shade ("net.harawata:appdirs:1.0.0") {
		transitive=false
	}
	
	tinyfd ("org.lwjgl:lwjgl-tinyfd:3.2.2:natives-linux") {
		transitive=false
	}
	tinyfd ("org.lwjgl:lwjgl-tinyfd:3.2.2:natives-macos") {
		transitive=false
	}
	tinyfd ("org.lwjgl:lwjgl-tinyfd:3.2.2:natives-windows") {
		transitive=false
	}

	implementation fileTree(dir: "libs", includes: ["*jar"])
}





def lavaplayerLibraries() {
	final def buildDir = project(":musicplayer-lavaplayer").layout.buildDirectory
	final def libs = buildDir.dir("libs").get().asFile.canonicalPath
	final def dependencies = buildDir.dir("dependencies").get().asFile.canonicalPath
	return "${libs};${dependencies}"
}